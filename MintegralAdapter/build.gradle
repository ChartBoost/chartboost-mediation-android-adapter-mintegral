plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'com.jfrog.artifactory'
}

repositories {
    google()
    mavenCentral()
    maven {
        url 'https://cboost.jfrog.io/artifactory/private-helium/'
        credentials {
            username System.getenv("JFROG_USER")
            password System.getenv("JFROG_PASS")
        }
    }
    maven {
        url 'https://cboost.jfrog.io/artifactory/helium/'
    }
    maven {
        url "https://dl-maven-android.mintegral.com/repository/mbridge_android_sdk_oversea"
    }
}

android {
    namespace "com.chartboost.helium.mintegraladapter"
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        versionCode 1
        // If you touch the following line, don't forget to update scripts/get_rc_version.zsh
        versionName System.getenv("VERSION_OVERRIDE") ?: "4.16.0.31.0"
        buildConfigField "String", "HELIUM_MINTEGRAL_ADAPTER_VERSION", "\"$versionName\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "location"
    productFlavors {
        local {}
        remote {}
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    localImplementation project(path: ':Helium')

    // For external usage, please use the following production dependency.
    // You may choose a different release version.
    // TODO: Change this to 4.+ when it's released
    remoteImplementation 'com.chartboost:helium:+'

    // Partner SDK
    implementation "com.mbridge.msdk.oversea:videojs:16.0.31"
    implementation "com.mbridge.msdk.oversea:mbjscommon:16.0.31"
    implementation "com.mbridge.msdk.oversea:playercommon:16.0.31"
    implementation "com.mbridge.msdk.oversea:reward:16.0.31"
    implementation "com.mbridge.msdk.oversea:videocommon:16.0.31"
    implementation "com.mbridge.msdk.oversea:same:16.0.31"
    implementation "com.mbridge.msdk.oversea:interstitialvideo:16.0.31"
    implementation "com.mbridge.msdk.oversea:mbnative:16.0.31"
    implementation "com.mbridge.msdk.oversea:nativeex:16.0.31"
    implementation "com.mbridge.msdk.oversea:mbbanner:16.0.31"
    implementation "com.mbridge.msdk.oversea:mbbid:16.0.31"

    // Adapter Dependencies
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'
}

artifactory {
    clientConfig.setIncludeEnvVars(true)

    contextUrl = 'https://cboost.jfrog.io/artifactory'
    publish {
        repository {
            // If this is a release build, push to the public "helium" artifactory.
            // Otherwise, push to the "private-helium" artifactory.
            var isReleaseBuild = 'true' == System.getenv('HELIUM_IS_RELEASE')
            if (isReleaseBuild) {
                repoKey = 'helium'
            } else {
                repoKey = 'private-helium'
            }
            // Set the environment variables for these to be able to push to artifactory.
            username = System.getenv("JFROG_USER")
            password = System.getenv("JFROG_PASS")
        }

        defaults {
            publications('MintegralAdapter', 'aar')
            publishArtifacts = true
            publishPom = true
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            remoteRelease(MavenPublication) {
                from components.remoteRelease

                var adapterName = "mintegral"
                groupId = 'com.chartboost'
                artifactId = "helium-adapter-$adapterName"
                version = project.hasProperty('snapshot') ? android.defaultConfig.versionName + rootProject.SNAPSHOT : android.defaultConfig.versionName

                pom {
                    name = "Helium Adapter Mintegral"
                    description = 'Better monetization. Powered by bidding'
                    url = 'https://www.chartboost.com/helium/'

                    licenses {
                        license {
                            name = 'https://answers.chartboost.com/en-us/articles/200780239'
                        }
                    }

                    developers {
                        developer {
                            id = 'chartboostmobile'
                            name = 'chartboost mobile'
                            email = 'mobile@chartboost.com'
                        }
                    }

                    scm {
                        var gitUrl = "https://github.com/ChartBoost/helium-android-adapter-$adapterName"
                        url = gitUrl
                        connection = gitUrl
                        developerConnection = gitUrl
                    }
                }
            }
        }
    }

    artifactoryPublish {
        publications(publishing.publications.remoteRelease)
    }
}
